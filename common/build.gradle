plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'org.spongepowered.gradle.vanilla'
}
base {
    archivesName = "${archives_base_name}-common"
}
minecraft {
    version(minecraft_version)
    if(file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}

dependencies {
    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    compileOnly group:'com.google.auto.service', name:'auto-service', version:'1.0.1'
    annotationProcessor group:'com.google.auto.service', name:'auto-service', version:'1.0.1'

    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"
}

tasks.register('apiJar', Jar) {
    archiveClassifier = "api"
    include "dev/greenhouseteam/enchantmentconfig/api/**"
    include "dev/greenhouseteam/enchantmentconfig/mixin/api/**"
    include "enchantmentconfigapi.mixins.json"
    include "pack.mcmeta"
}

build.dependsOn apiJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}