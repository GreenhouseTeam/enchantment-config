plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}
base {
    archivesName = "${archives_base_name}-common"
}
sourceSets {
    api {
        java
        resources
        compileClasspath += sourceSets.main.compileClasspath
    }
    main {
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

configurations {
    api
}

minecraft {
    version(minecraft_version)
    if(file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}

dependencies {
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))

    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

tasks.register('apiJar', Jar) {
    archiveClassifier = "api"
    dependsOn(compileApiJava)
    dependsOn(processApiResources)
    from sourceSets.api.output.classesDirs
    from sourceSets.api.output.resourcesDir
    filesMatching(['pack.mcmeta', '*.mixins.json']) {
        expand project.properties
    }
}

artifacts {
    api apiJar
}

assemble.dependsOn apiJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
            artifact(apiJar) {
                builtBy apiJar
                classifier 'api'
            }
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
    }
}